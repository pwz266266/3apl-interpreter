Name: Cleaner

Belief Base:
    positionAt(self,0,0).
    positionAt(rubbish,1,2).
    positionAt(rubbish,0,4).
    positionAt(bin,5,6).
Goal Base:
    clean()

Capability Base:
    {((positionAt(self,X,Y) and positionAt(rubbish,X,Y)) and not occupied())}
    pick(X,Y)
    {not positionAt(rubbish,X,Y), occupied()}

    {positionAt(self,X0,Y0)}
    moveTo(X1,Y1)
    {not positionAt(self,X0,Y0), positionAt(self,X1,Y1)}

    {((positionAt(self,X,Y) and positionAt(bin,X,Y)) and occupied())}
    throw()
    {not occupied()}

Plan Base:

Goal Planning Rule Base:
    {
        clean()
    }
    <- positionAt(rubbish, _, _) |
    {
        Test: positionAt(bin,Xb,Yb);
        while(positionAt(rubbish,X,Y)) {
            moveTo(X,Y);
            pick(X,Y);
            moveTo(Xb, Yb);
            throw();
        };
        moveTo(0,0);
    }

Plan Revision Rule Base:
   {
       while(positionAt(rubbish,X,Y)) {
           moveTo(X,Y);
           pick(X,Y);
           moveTo(Xb, Yb);
           throw();
       };
   }
   <- received(AMS, 0, inform, description_terminate) |
   {
        if (occupied())
        {
            moveTo(Xb,Yb);
            throw();
        }
        else
        {
            Test: true;
        };
   }