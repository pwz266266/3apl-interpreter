Name: Cleaner
Belief Base:
    positionAt(self,0,0).
    positionAt(rubbish,1,2).
    positionAt(rubbish,0,4).
    positionAt(bin,5,6).
Goal Base:
    clean();
Capabilities:
    {positionAt(self,X,Y) and positionAt(rubbish,X,Y) and not occupied()}
    pick(X,Y)
    {not positionAt(rubbish,X,Y), occupied()}

    {positionAt(self,X0,Y0)}
    moveTo(X1,Y1)
    {not positionAt(self,X0,Y0), positionAt(self,X1,Y1)}

    {positionAt(self,X,Y), positionAt(bin,X,Y), occupied()}
    throw()
    {not occupied()}

Plan Base:

Goal Planning Rules:
    {
        clean()
    }
    <- positionAt(rubbish, _, _) |
    {
        positionAt(bin,Xb,Yb)?
        while(positionAt(rubbish,X,Y)) do {
            moveTo(X,Y)
            pick(X,Y)
            moveTo(Xb, Yb)
            throw()
        }
        moveTo(0,0)
    }
Plan Revision Rules:
   {
       while(positionAt(rubbish,X,Y)) do {
           moveTo(X,Y)
           pick(X,Y)
           moveTo(Xb, Yb)
           throw()
       }
   }
   <- received(AMS, 0, inform, description(terminate)) |
   {
        if (occupied()) then
        {
            moveTo(Xb,Yb)
            throw()
        }
        else{}
   }