
options
{
  LOOKAHEAD= 1;

  CHOICE_AMBIGUITY_CHECK = 2;
  OTHER_AMBIGUITY_CHECK = 1;
  STATIC = false;
  DEBUG_PARSER = false;
  DEBUG_LOOKAHEAD = false;
  DEBUG_TOKEN_MANAGER = false;
  ERROR_REPORTING = true;
  JAVA_UNICODE_ESCAPE = false;
  UNICODE_INPUT = false;
  IGNORE_CASE = false;
  USER_TOKEN_MANAGER = false;
  USER_CHAR_STREAM = false;
  BUILD_PARSER = true;
  BUILD_TOKEN_MANAGER = true;
  SANITY_CHECK = true;
  FORCE_LA_CHECK = true;
}

PARSER_BEGIN(plan_parser)
import java.util.ArrayList;
import alice.tuprolog.*;
import java.io.*;
public class plan_parser
{
 public static void main(String args[]) throws ParseException, FileNotFoundException {
    plan_parser parser = new plan_parser(new FileInputStream("./src/PlanParserTest1.txt"));
    Plan plan = parser.plan();
    System.out.println(plan.toString());
 }
}
PARSER_END(plan_parser)
SKIP :
{
        " "
    |   "\r"
    |   "\t"
    |   "\n"
    |   "\r\n"
}
TOKEN :
{
      <NAME: "Name">
    | <CAPBASE: "Capability Base">
    | <BELBASE: "Belief Base">
    | <GOALBASE: "Goal Base">
    | <PLANBASE: "Plan Base">
    | <GP_RULES: "Goal Planning Rule Base">
    | <PR_RULES: "Plan revision Rule Base">
    | <COMMA: ",">
    | <COLON: ":">
    | <PROLOGEND: ".">
    | <LBRACE: "{">
    | <RBRACE: "}">
    | <AND: "and">
    | <OR: "or">
    | <IF: "if">
    | <WHILE: "while">
    | <DO: "do">
    | <THEN: "then">
    | <ELSE: "else">
    | <SEMICOL: ";">
    | <NONE: "None">
    | <SEND: "Send">
    | <JAVA: "Java">
    | <LPAR: "(">
    | <RPAR: ")">
    | <VBAR: "|">
    | <QUSMARK: "?">
    | <TRUE: "true">
    | <NOT: "not">
    | <IMPLY: "<-">
    | <GIVES: ":-">
    | <VAL: (((["a"-"z"]) (["A"-"Z"] | ["a"-"z"] | "_" | ["0" - "9"])*) | (["0" - "9"])+)>
    | <VAR: ((["A"-"Z"] | "_") (["A"-"Z"] | ["a"-"z"] | "_" | ["0" - "9"])*)>

}




PlanBase plans():
{
    Plan buffer;
    ArrayList<Plan> plans = new ArrayList<Plan>();
}
{
    (buffer = plan() {plans.add(buffer);})*
    {return new PlanBase(plans);}
}

Plan plan():
{
    SeqPlan plan;
}
{
    <LBRACE> plan = seqPlan() <RBRACE> {return new Plan(plan);}
}


SeqPlan seqPlan():
{
    BasicPlan buffer;
    ArrayList<BasicPlan> components = new ArrayList<BasicPlan>();
}
{
     (buffer = basicplan() {components.add(buffer);} <SEMICOL>)+ {return new SeqPlan(components);}
}

BasicPlan basicplan():
{
    Query testbuffer;
    ArrayList<Atom> arguments = new ArrayList<Atom>();
    ArrayList<BasicPlan> components = new ArrayList<BasicPlan>();
    BasicPlan buffer;
    Atom argbuffer;
    Token t;
    SeqPlan ifPlan;
    SeqPlan elsePlan;
}
{
    (<JAVA> <LPAR> ((argbuffer = atom() {arguments.add(argbuffer);}) (<COMMA>argbuffer = atom() {arguments.add(argbuffer);})*)? <RPAR> {return new JavaAction(arguments);})
    | LOOKAHEAD(query() <QUSMARK> <SEMICOL> ) (testbuffer = query() <QUSMARK> {return new TestAction(testbuffer); } )
    | (<SEND> <LPAR> ((argbuffer = atom() {arguments.add(argbuffer);}) (<COMMA>argbuffer = atom() {arguments.add(argbuffer);})*)? <RPAR>{return new SendAction(arguments);})
    | ( t = <VAL> <LPAR> ((argbuffer = atom() {arguments.add(argbuffer);}) (<COMMA>argbuffer = atom() {arguments.add(argbuffer);})*)? <RPAR> {return new CapAction(t.image, arguments);})
    | (<WHILE> <LPAR> testbuffer = query() <RPAR> <LBRACE> (buffer = basicplan() {components.add(buffer);} <SEMICOL>)+ <RBRACE> {return new WhilePlan(components, testbuffer);} )
    | (<IF> <LPAR> testbuffer = query() <RPAR> <LBRACE> ifPlan = seqPlan() <RBRACE> <ELSE> <LBRACE> elsePlan = seqPlan() <RBRACE> {return new IfPlan(ifPlan, elsePlan, testbuffer);})
}







ArrayList<Literal> literals():
{
    Literal buffer;
    ArrayList<Literal> result = new ArrayList<Literal>();
}
{
    buffer = literal() {result.add(buffer);} (<COMMA> buffer = literal() {result.add(buffer);})*
    {return result;}
}





Query query():
{
    Query sub0;
    Query sub1;
    boolean isOr;
}
{
    (sub0 = singlequery() {return sub0;})
    | (<LPAR> sub0 = query() ((<OR> sub1 = query() <RPAR> {return new wffBinary(true, sub0, sub1);}) |
    (<AND> sub1 = query() <RPAR> {return new wffBinary(false, sub0, sub1);})))
}


Query singlequery():
{
    Query query;
}
{
    (query = truequery() {return query;}) | (query = literal() {return query;})
}


TrueQuery truequery():
{
}
{
    <TRUE> {return new TrueQuery();}
}


Literal literal():
{
    VpredClause clause;
}
{
    (clause = vpredclause() {return new Literal(false, clause);}) | (<NOT> clause = vpredclause() {return new Literal(true, clause);})
}


GpredClause gpredclause():
{
    Token t;
    ArrayList<Atom> arguments = new ArrayList<Atom>();
    Atom buffer;
}
{
    t = <VAL> <LPAR> (buffer = val() {arguments.add(buffer);} )? (<COMMA> buffer = val() {arguments.add(buffer);})* <RPAR>
    {return new GpredClause(t.image, arguments);}
}


VpredClause vpredclause():
{
    Token t;
    ArrayList<Atom> arguments = new ArrayList<Atom>();
    Atom buffer;
}
{
    t = <VAL> <LPAR> (buffer = atom() {arguments.add(buffer);} )? (<COMMA> buffer = atom() {arguments.add(buffer);})* <RPAR>
    {return new VpredClause(t.image, arguments);}
}


Atom val():
{
    Token t;
}
{
    t = <VAL>{return new Atom(t.image);}
}


Atom var():
{
    Token t;
}
{
    t = <VAR>{return new Atom(t.image);}
}


Atom atom():
{
    Atom a;
}
{
    (a = var() {return a;})| (a = val() {return a;})
}